plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'dev.pcvolkmer.mv64e'
version = '0.1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    api 'com.fasterxml.jackson.core:jackson-databind:2.12.7.2'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

publishing {
    repositories {
        maven {
            name = "GitDnpmDev"

            def releasesRepoUrl = uri('https://git.dnpm.dev/api/packages/public/maven')
            def snapshotsRepoUrl = uri('https://git.dnpm.dev/api/packages/public-snapshots/maven')
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials(HttpHeaderCredentials) {
                name = "Authorization"
                value = "token ${dnpm_dev_token}"
            }

            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = archivesBaseName
        }
    }
}
